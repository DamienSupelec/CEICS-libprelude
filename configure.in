dnl Process this file with autoconf to produce a configure script.

AC_INIT(src)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(libprelude, 0.4.1)
AM_CONFIG_HEADER(config.h)
AM_DISABLE_STATIC

AM_MAINTAINER_MODE

AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_C_INLINE
AC_C_BIGENDIAN

AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(libprelude_libs)
AC_SUBST(libprelude_cflags)


dnl **************************************************
dnl * Check for demand loading of shared lib library *
dnl **************************************************
AC_CHECK_LIB(dl, dlopen, libprelude_libs="-ldl")



dnl **************************************************
dnl * Check for crypt library                        *
dnl **************************************************
AC_CHECK_LIB(crypt, crypt, cryptlib=true)

if test x$cryptlib = xtrue; then
	AC_DEFINE(HAVE_CRYPT)
	libprelude_libs="$libprelude_libs -lcrypt";
fi



dnl **************************************************
dnl * Check for Pthread library                      *
dnl **************************************************

AC_CHECK_LIB(pthread, pthread_create, pthrlib="yes")
if test x$pthrlib = xyes; then
	libprelude_libs="$libprelude_libs -lpthread"
	libprelude_cflags="-D_REENTRANT"
else
	AC_CHECK_LIB(c_r, pthread_create, pthrlib="yes")
	if test x$pthrlib = xyes; then
		libprelude_libs="$libprelude_libs -lc_r"
		libprelude_cflags="-D_REENTRANT"
	fi
fi



dnl **************************************************
dnl * Check for networking function                  *
dnl **************************************************

AC_CHECK_LIB(c, inet_ntoa, libc_inet_ntoa=yes)
if test x$libc_inet_ntoa != xyes; then
	AC_CHECK_LIB(nsl, inet_ntoa, libprelude_libs="$libprelude_libs -lnsl")
fi

AC_CHECK_LIB(c, socket, libc_socket=yes)
if test x$libc_socket != xyes; then
	AC_CHECK_LIB(socket, socket, libprelude_libs="$libprelude_libs -lsocket")
fi

AC_CHECK_LIB(c, inet_aton, libc_inet_aton=yes)
if test x$libc_inet_aton != xyes; then
	AC_CHECK_LIB(resolv, inet_aton, libprelude_libs="$libprelude_libs -lresolv")
fi




dnl **************************************************
dnl * Check for OpenSSL.                             *
dnl **************************************************

AC_CHECK_HEADER(openssl/ssl.h, openssl_header=yes)

AC_ARG_ENABLE(openssl, [  --enable-openssl Use OpenSSL to communicate with Report Server [default=auto]], \
	      enable_openssl="$enableval", enable_openssl=auto)

if test x$enable_openssl = xauto ; then
	if test x$openssl_header = xyes; then
		libprelude_libs="$libprelude_libs -lssl -lcrypto"
		AC_DEFINE(HAVE_SSL)
    		enable_openssl=yes
  	else
    		enable_openssl=no
  	fi
fi


dnl **************************************************
dnl * Enable / Disable gprof profiling.              *
dnl **************************************************

AC_ARG_ENABLE(profiling, [  --enable-profiling Enable Gprof profiling - with thread workaround [default=no]], \
	      enable_profiling=yes, enable_profiling=no)

if test x$enable_profiling = xyes ; then
	CFLAGS="$CFLAGS -O0 -pg"	
	AC_DEFINE(ENABLE_PROFILING)

	dnl *** for gcc3 ***
	
	cat > conftest.c << EOF 

	int main(void)
	{
		exit(0);
	}
EOF

	AC_MSG_CHECKING(whether compiler support -fprofile-arcs)

        ${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.c $LIBS >/dev/null 2>&1
	if test -x conftest ; then
        	CFLAGS="$CFLAGS -fprofile-arcs";
		AC_MSG_RESULT(yes);
		rm -f conftest;
	else
		AC_MSG_RESULT(no);
	fi
fi



dnl **************************************************
dnl * Check for GTK-DOC                              *
dnl **************************************************

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
	HTML_DIR='${datadir}/gtk-doc/html';
else
	HTML_DIR=$with_html_dir;
fi

AC_SUBST(HTML_DIR)
AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)

gtk_doc_min_version=0.6

if $GTKDOC; then 
	gtk_doc_version=`gtkdoc-mkdb --version`
	AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])

    	if perl << EOF ; then
      		exit (("$gtk_doc_version" =~ /^[[0-9]]+\.[[0-9]]+$/) && ("$gtk_doc_version" >= "$gtk_doc_min_version") ? 0 : 1); 
EOF
      		AC_MSG_RESULT(yes);
   	else
      		AC_MSG_RESULT(no);
      		GTKDOC=false;
   	fi
fi


dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=auto)

if test x$enable_gtk_doc = xauto; then
  	if test x$GTKDOC = xtrue; then
    		enable_gtk_doc=yes;
  	else
    		enable_gtk_doc=no;
  	fi
fi

AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)



dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday select socket strdup strerror strstr)

sensors_config_dir=$sysconfdir/prelude-sensors
AC_SUBST(sensors_config_dir)

if test x$enable_profiling = xno; then
	CFLAGS="$CFLAGS -O2 -fomit-frame-pointer"
fi

CFLAGS="$CFLAGS -Wall -Wstrict-prototypes -Wmissing-prototypes -Wmissing-declarations \
-Wbad-function-cast -Wcast-qual -Wcast-align -Wnested-externs -Wunused"


AC_SUBST(CFLAGS)

AC_OUTPUT([

Makefile 
libprelude-config

docs/Makefile
docs/api/Makefile

src/Makefile 
src/include/Makefile

], [ chmod +x libprelude-config ])


echo
echo "*** Dumping configuration ***"
echo "    - Use OpenSSL            : $enable_openssl"
echo "    - Generate documentation : $enable_gtk_doc"
echo "    - Profiling enabled      : $enable_profiling"









